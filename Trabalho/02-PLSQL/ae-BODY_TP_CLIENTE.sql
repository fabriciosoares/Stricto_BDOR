CREATE OR REPLACE TYPE BODY TP_CLIENTE AS
	MEMBER FUNCTION EBAIXARENDA RETURN VARCHAR2 AS RETORNO VARCHAR2(555);
	BEGIN
		IF ((SELF.RENDA IS NOT NULL) AND SELF.RENDA <= 700.00) THEN
			RETORNO:='É CLIENTE DE BAIXA RENDA.';
		ELSE
			RETORNO:='NÃO É CLIENTE DE BAIXA RENDA.';
		END IF;
		RETURN RETORNO;
	END EBAIXARENDA;
	MEMBER FUNCTION GETRENDA RETURN VARCHAR2 AS RETORNO VARCHAR2(555);
	BEGIN
		IF(SELF.RENDA IS NOT NULL) THEN
			RETORNO:=TO_CHAR(SELF.RENDA);
		ELSE
			RETORNO:='NENHUMA RENDA DECLARADA';
		END IF;
		RETURN RETORNO;
	END GETRENDA;
	MEMBER FUNCTION GETINFO RETURN VARCHAR2 AS RETORNO VARCHAR2(855);
	BEGIN
		SELECT
			' CÓDIGO DO CLIENTE: ' || TO_CHAR(SELF.COD) ||
			' DATA DE CADASTRO: ' || TO_CHAR(SELF.DATAREGISTRO,'DD/MM/YYYY') ||
			' RENDA DECLARADA: ' || SELF.GETRENDA() ||
			' (' || SELF.EBAIXARENDA() || ') ' ||
			' DADOS DO CLIENTE: ' || (DEREF(SELF.REF_CLIENTE).GETINFO())
			INTO RETORNO FROM DUAL;
		RETURN RETORNO;
	END GETINFO;
	ORDER MEMBER FUNCTION EQUALS(OBJ TP_CLIENTE) RETURN NUMBER AS
	BEGIN
		RETURN  (SELF.COD  - OBJ.COD); -- ZERO: SELF > OBJ | NEGATIVO: SELF < OBJ | POSITIVO: SELF > OBJ
	END EQUALS;
	STATIC FUNCTION COMPARE(OBJ1 TP_CLIENTE, OBJ2 TP_CLIENTE) RETURN BOOLEAN AS RETORNO BOOLEAN;
	BEGIN
		RETORNO := FALSE;
		IF(TO_CHAR(OBJ1.COD) = TO_CHAR(OBJ2.COD)) THEN
			RETORNO := TRUE;
		ELSE
			RETORNO := FALSE;
		END IF;
		RETURN RETORNO;
	END COMPARE;
	CONSTRUCTOR FUNCTION TP_CLIENTE(CPF_CNPJ IN VARCHAR2) RETURN SELF AS RESULT AS REFCLIENTE REF TP_PESSOA;
	CODPESSOA NUMBER;
	V_CPF_CNPJ VARCHAR2(14);
	BEGIN
		V_CPF_CNPJ:=CPF_CNPJ;
		IF(VALIDA_CPF(V_CPF_CNPJ) = 'SIM') THEN
			SELECT REF(PF), PF.COD INTO REFCLIENTE, CODPESSOA FROM TB_FISICA PF WHERE PF.CPF=V_CPF_CNPJ;
		ELSIF(VALIDA_CNPJ(V_CPF_CNPJ) = 'SIM') THEN
			SELECT REF(PJ), PJ.COD INTO REFCLIENTE, CODPESSOA FROM TB_JURIDICA PJ WHERE PJ.CNPJ=V_CPF_CNPJ;
		END IF;
		SELF.COD := CODPESSOA;
		SELF.REF_CLIENTE := REFCLIENTE;
		SELF.DATAREGISTRO := SYSDATE;
		SELF.RENDA:=NULL;
		RETURN;
	END TP_CLIENTE;
	CONSTRUCTOR FUNCTION TP_CLIENTE(CPF IN VARCHAR2, RENDA IN NUMBER) RETURN SELF AS RESULT AS REFCLIENTEPESSOAFISICA REF TP_FISICA;
	CODPESSOA NUMBER;
	V_CPF VARCHAR2(11);
	BEGIN
		V_CPF:=CPF;
		SELECT REF(PF), PF.COD INTO REFCLIENTEPESSOAFISICA, CODPESSOA FROM TB_FISICA PF WHERE PF.CPF=V_CPF;
		SELF.COD := CODPESSOA;
		SELF.REF_CLIENTE := REFCLIENTEPESSOAFISICA;
		SELF.DATAREGISTRO := SYSDATE;
		SELF.RENDA := RENDA;
		RETURN;
	END TP_CLIENTE;
END;